buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'uk.gov.hmcts.java' version '0.12.18'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.5.9'
    id 'org.owasp.dependencycheck' version '6.0.3'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id "info.solidsoft.pitest" version '1.6.0'
    id 'org.sonarqube' version '3.1.1'
    id 'com.gorylenko.gradle-git-properties' version '2.2.2'
    id "io.freefair.lombok" version "5.3.0"
}

apply plugin: "com.gorylenko.gradle-git-properties"

group = 'uk.gov.hmcts.reform'
version = '0.1.0'

allprojects {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/reform/pcqloader/gitcommit")
    keys = ['git.commit.id','git.commit.time']
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "GMT"
}

sourceSets {
    test {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/test/java')
        }
        resources.srcDir file('src/test/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntime.extendsFrom testRuntime
    smokeTestRuntime.extendsFrom testRuntime
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task unit(type: Test, description: 'Runs the unit tests.', group: 'Verification') {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}

task functional() {
  // This task is required for a Jenkins hook (fullFunctionalTest)
}

task functionalPostDeploy(type: Test) {
    group = 'Functional Tests'
    description = 'Verifies that files from functionPreDeploy step were processed following job creation in AKS'
    setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
    setClasspath(sourceSets.functionalTest.runtimeClasspath)
    include "uk/gov/hmcts/reform/pcqloader/postdeploy/**"
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDirectory = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.28.0"
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    // https://github.com/pmd/pmd/issues/876
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

project.tasks['jacocoTestReport'].dependsOn test, integration

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ['uk.gov.hmcts.reform.pcqloader.*']
    threads = 10
    enableDefaultIncrementalAnalysis = true
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
    mutationThreshold = 50
    timeoutConstInMillis = 30000

}

project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "Reform :: pcq-loader"
        property "sonar.projectKey", "uk.gov.hmcts.reform:pcq-loader"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all {
            def isNonStable = { String version ->
                ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                    version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
                }
            }
            if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
                selection.reject('Release candidate')
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'

    analyzers {
      // Disable scanning of .NET related binaries
      assemblyEnabled = false
    }
}

dependencyManagement {
    dependencies {
        dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.69'
        // CVE-2018-10237 - Unbounded memory allocation
        // CVE-2020-8908 - Temp directory creation vulnerability
        dependencySet(group: 'com.google.guava', version: '30.1-jre') {
          entry 'guava'
        }

        // CVE-2021-29425
        dependency group: 'commons-io', name: 'commons-io', version: '2.11.0'

        dependencySet(group: 'org.springframework.cloud', version: '3.0.5') {
          entry 'spring-cloud-starter-openfeign'
          entry 'spring-cloud-openfeign-core'
        }

        dependencySet(group: 'org.springframework.security', version: '5.6.1') {
          entry 'spring-security-core'
          entry 'spring-security-test'
        }
    }
    imports {
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.3'
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

def versions = [
        junit               : '5.7.0',
        reformLogging       : '5.1.1',
        springBoot          : springBoot.class.package.implementationVersion,
        springfoxSwagger    : '3.0.0',
        restAssured         : '4.2.0',
        lombok              : '1.18.12',
        gradlePitest        : '1.6.0',
        pitest              : '1.6.3',
        sonarPitest         : '0.5',
        testcontainers      : '1.15.1',
        springFramework     : '5.3.15'
]

ext['spring-framework.version'] = '5.3.12'

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['org.apache.logging.log4j']) {
                details.useVersion '2.17.1'
            }
            if (details.requested.group in ['org.scala-lang']) {
                details.useVersion '2.12.10'
            }
            if (details.requested.group in ['org.apache.tomcat.embed']) {
                details.useVersion '9.0.59'
            }
            if (details.requested.group in ['org.apache.httpcomponents.httpclient']) {
              details.useVersion '4.5.13'
            }
        }
    }
}

dependencies {
    // exclude spring-cloud-context as it is already included in spring-cloud-starter-netflix-hystrix
    def withoutSpringCloudContext = {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-context'
    }

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web-services'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    implementation group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'

    implementation group: 'org.springframework', name:'spring-aop', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-beans', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-context', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-core', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-expression', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-jcl', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-oxm', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-web', version: versions.springFramework
    implementation group: 'org.springframework', name:'spring-webmvc', version: versions.springFramework

    implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.14.1'

    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '11.0'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.0.5'

    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: "com.networknt", name: "json-schema-validator", version: "1.0.31"

    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
    implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: '3.8.0'
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

    implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.1.0', withoutSpringCloudContext
    implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
    implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
    implementation group: "com.gilecode.yagson", name:"j9-reflection-utils", version:"1.0"

    implementation group: "io.jsonwebtoken", name: "jjwt", version: "0.9.1"
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'com.github.hmcts', name: 'pcq-commons', version: '1.1.5'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.13.2'


    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'org.springframework.security', name: 'spring-security-test'

    testImplementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    testImplementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.7'
    testImplementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

    testImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
    testImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssured

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.4.6'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.4.6'

    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.19.0'

    testImplementation 'com.github.hmcts:fortify-client:1.2.0:all'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'
    testImplementation group: "com.gilecode.yagson", name:"j9-reflection-utils", version:"1.0"
    testImplementation group: "io.jsonwebtoken", name: "jjwt", version: "0.9.1"
    testImplementation group: "org.testcontainers", name: "junit-jupiter", version: versions.testcontainers

    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath

    functionalTestImplementation sourceSets.main.runtimeClasspath
    functionalTestImplementation sourceSets.test.runtimeClasspath
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    smokeTestRuntime group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
    smokeTestRuntime group: 'io.rest-assured', name: 'json-path', version: versions.restAssured
    smokeTestRuntime group: 'io.rest-assured', name: 'kotlin-extensions', version: versions.restAssured

    smokeTestImplementation sourceSets.main.runtimeClasspath
    smokeTestImplementation sourceSets.test.runtimeClasspath
}

test {
    useJUnitPlatform()
    failFast = true
}

mainClassName = 'uk.gov.hmcts.reform.pcqloader.PcqLoaderApplication'

bootJar {
    archiveName = 'pcq-loader.jar'

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

rootProject.tasks.named("processTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processResources") {
  dependsOn("generateGitProperties")
}
